! ====================================
 DEFINICIÓN DE CONJUNTOS Y RELACIONES
 =====================================;
SETS:
!INDICES: i=producto j=planta_elaboradora k=centro;
    ! Productos (i): 1. leche entera, 2. descremada, 3. en polvo entera, 4. en polvo descremada, 5. crema, 6. manteca;
    ! Atributos: 
    ! - DEMANDA_PRODUCTO - D[i]: demanda general de cada tipo de producto, 
    ! - FACTOR_REFRIGERACION_PRODUCTO - δ[i]: factor que determina los costos adicionales de tranporte para productos refrigerados por planta;
    PRODUCTOS /1..6/: DEMANDA_PRODUCTO, FACTOR_REFRIGERACION_PRODUCTO;       

    ! Plantas elaboradoras (j): 1. Buenos Aires, 2. Córdoba, 3. Santa Fe
    Atributos:
    - LITROS_LECHE_DESCREMADA_SANTA_FE - M[j]: litros de leche descremada pasteurizada enviados desde las otras plantas a la planta de Santa Fe (P3) j={1,2}, 
    - LITROS_ENTERA_A_DESCREMADA - L[j]: litros de leche entera destinados a la leche descremada en todas las plantas j={1,2} ,
    - LITROS_CREMA - C[j]: litros de crema producidos en todas las plantas j={1,2};
    PLANTAS /1..3/: LITROS_LECHE_DESCREMADA_SANTA_FE, LITROS_ENTERA_A_DESCREMADA, LITROS_CREMA;

    ! Centros de distribución (k): 1. CABA, 2. Rosario, 3. Mendoza, 4. Tucumán;
    ! Atributos:
    ! - FRACCION_DEMANDA_CENTRO - q[k]: fracción de la demanda total según cada centro de distribución;
    CENTROS /1..4/: FRACCION_DEMANDA_CENTRO;

!Variables de decisión;
    !stock al final del periodo de productos en plantas 
    (aplica a 3. leche en polvo entera y 4. leche en polvo descremada);
    STOCK(PRODUCTOS, PLANTAS): S;                     
    !cantidad de producto i producido en j destinado a k;
    CANTIDAD_PRODUCTO(PRODUCTOS, PLANTAS, CENTROS): Q;
!Conjuntos de datos a utilizar;
    ! Relación planta-centro: distancia y costo base transporte;
    PLXCN(PLANTAS, CENTROS): DISTANCIA, COSTO_TRANSPORTE;    
    ! Subconjunto para productos con capacidad (Leches fluidas agrupadas y leches en polvo agrupadas);
    PRODUCTOSAUX /1..4/;                       
    ! Capacidad de producción por planta para productos agrupados;
    PRODUXPL(PRODUCTOSAUX, PLANTAS): CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS;   
ENDSETS

DATA:

! Importar datos desde archivo usando registros separados por ~;
! 1. DEMANDA_PRODUCTO i = {1,2,3,4,5,6}
! 2. FACTOR_REFRIGERACION_PRODUCTO i = {1,2,3,4,5,6}
! 3. FRACCION_DEMANDA_CENTRO k = {1,2,3,4}
! 4. DISTANCIA (12 valores - j x k, con j = {1,2,3} k = {1,2,3,4})
! 5. COSTO_TRANSPORTE (12 valores - j x k, con j = {1,2,3} k = {1,2,3,4})
! 6. CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS (12 valores - j x k, con j = {1,2,3} k = {1,2,3,4})
! 7. DISPONIBILIDAD_LECHE (1 valor);
DEMANDA_PRODUCTO = @FILE('data.ldt');
FACTOR_REFRIGERACION_PRODUCTO = @FILE('data.ldt');
FRACCION_DEMANDA_CENTRO = @FILE('data.ldt');
DISTANCIA = @FILE('data.ldt');
COSTO_TRANSPORTE = @FILE('data.ldt');
CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS = @FILE('data.ldt');
DISPONIBILIDAD_LECHE = @FILE('data.ldt');

! Definición de constantes;

! Costo de envío de leche descremada a Santa Fe, por litro;
COSTO_ENVIO_SANTA_FE = 20;

!  Factor de conversión de litros de leche cruda a cantidades de productos;
!  1 unidad de producto = X cantidad de litros de leche cruda;
CONVERSION_LECHE_ENTERA = 1.063; 
CONVERSION_LECHE_DESCREMADA = 1.063;
CONVERSION_LECHE_EN_POLVO_ENTERA_Y_SEMIDESCREMADA = 8.755;
CONVERSION_LECHE_DESNATADA = 11.525;
CONVERSION_CREMA = 0.08;
CONVERSION_CREMA_MANTECA = 2.77;

ENDDATA

!FUNCION OBJETIVO:;
[FO] MIN = @SUM(PRODUCTOS(I): @SUM(PLANTAS(J): @SUM(CENTROS(K): FACTOR_REFRIGERACION_PRODUCTO(I) * COSTO_TRANSPORTE(J,K) * Q(I,J,K) * 1/1000))) + COSTO_ENVIO_SANTA_FE * @SUM(PLANTAS(J): LITROS_LECHE_DESCREMADA_SANTA_FE(J));

!1 - Satisfacción mínima de demanda por producto y centro;
@FOR(PRODUCTOS(I): 
    @FOR(CENTROS(K): 
        @SUM(PLANTAS(J): Q(I,J,K)) >= FRACCION_DEMANDA_CENTRO(K) * DEMANDA_PRODUCTO(I)
    )
);

!2 - Restricción de disponibilidad total de leche cruda;
! TODO: 
Primero dicen para todo i,j, pero luego hacen la sumatoria en j y i le fijan valores. 
    Si voy a fijar valores, entonces no es para todo i. Si hago una sumatoria, entonces no es para todo j. 
    Si digo que es para todo i,j digo que esta restricción se repite para cada combinación de (i,j) posible. 
    Pero no es lo que están queriendo decir. Me explico? Para todo i,j no va.;
@SUM(PLANTAS(J): @SUM(CENTROS(K): CONVERSION_LECHE_ENTERA * Q(1,J,K))) +
@SUM(PLANTAS(J): CONVERSION_LECHE_DESCREMADA * LITROS_ENTERA_A_DESCREMADA(J)) +
@SUM(PLANTAS(J): @SUM(CENTROS(K): CONVERSION_LECHE_EN_POLVO_ENTERA_Y_SEMIDESCREMADA * Q(3,J,K))) +
@SUM(PLANTAS(J): CONVERSION_LECHE_EN_POLVO_ENTERA_Y_SEMIDESCREMADA * S(3,J)) <= DISPONIBILIDAD_LECHE;

!3 - Definición de LITROS_ENTERA_A_DESCREMADA - L[j] para j en {1,2};
@FOR(PLANTAS(J):
    LITROS_ENTERA_A_DESCREMADA(J) = 
        @SUM(CENTROS(K): Q(2,J,K)) +
        LITROS_LECHE_DESCREMADA_SANTA_FE(J) +
        CONVERSION_LECHE_DESNATADA * @SUM(CENTROS(K): Q(4,J,K)) +
        CONVERSION_LECHE_DESNATADA * S(4,J)
);

!4 - Cantidad de leche desnatada en la planta 4;
! TODO: Entiendo que esta restricción sea solo para Santa Fe y esta bien poner para todo j=3, eso es correcto.
    Lo que me hace ruido es que la variable Mj esta sumando en j, entonces se ve rara la restricción.
    Se me ocurren dos opciones: a. Pueden poner en la sumatoria j=1 y arriba del signo de sumatoria 2 (para que la suma sea entre j={1,2}) o b. Llamar j' 
    a la j que esta dentro de la sumatoria, entonces sería sumatoria j' de Mj' - ...(todo lo que sigue igual como esta) para diferenciar que las j que se
    suman no es la j que se fija. No se si me explico bien. La opción b es la que yo usaría, pero si la hacen como a lo considero bien también.;
@SUM(PLANTAS(J): LITROS_LECHE_DESCREMADA_SANTA_FE(J)) - 
@SUM(CENTROS(K): Q(2,3,K)) - 
CONVERSION_LECHE_DESNATADA * S(4,3) = 0;

!5 - Crema - subproducto Cj para j en {1,2} - VER COMO RESTRINGIR;
! TODO: Pueden reemplazar Cj por la expresión de la restricción 6 y se ahorran una restricción. ;
! RO - Se puede utilizar la expresión del lado derecho de la igualdad para reemplazar el lado izquierdo de la desigualdad de la restricción 6;
@FOR(PLANTAS(JJ) | JJ #LT# 3:
    LITROS_CREMA(JJ) = CONVERSION_CREMA * LITROS_ENTERA_A_DESCREMADA(JJ)
);

! 6 - Definición del subproducto crema producido en las plantas j=1 (Buenos Aires) y j=2 (Córdoba);
! La cantidad de crema producida en las plantas j=1 y j=2 debe ser mayor o igual que la cantidad de 
! crema vendida como producto y usada como subproducto para producir manteca;
@FOR(PLANTAS(JJ) | JJ #LT# 3:
    LITROS_CREMA(JJ) >= 
        @SUM(CENTROS(K): Q(5,JJ,K)) + 
        CONVERSION_CREMA_MANTECA * @SUM(CENTROS(K): Q(6,JJ,K))
);

!7 - Restricción de disponibilidad de leche entera en plantas 1 y 2;
! TODO: estan considerando todos los productos, consideren solo los relacionados a leche fluida (entera, descremada y enviada a Santa Fe);
@FOR(PLANTAS(JJ) | JJ #LT# 3:
    @SUM(CENTROS(K): Q(1,JJ,K)) +
    LITROS_ENTERA_A_DESCREMADA(JJ) +
    CONVERSION_LECHE_DESNATADA * (
        @SUM(CENTROS(K): Q(3,JJ,K)) + 
        S(3,JJ)
    ) <= CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS(1,JJ)
);

!8 - Restricción de disponibilidad de leche cruda para planta 3;
! TODO: similar a la observación del punto anterior. Consideren solo la leche fluida entera;
@SUM(CENTROS(K): Q(1,3,K)) +
CONVERSION_LECHE_EN_POLVO_ENTERA_Y_SEMIDESCREMADA * (
    @SUM(CENTROS(K): Q(3,3,K)) +
    S(3,3)
) <= CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS(1,3);

! 9 - Restricción del subproducto crema producido en las plantas j=1 (Buenos Aires) y j=2 (Córdoba);
! La capacidad máxima de producto agrupado de índice 3 corresponde a la capacidad de producción 
! de crema para las plantas j=1 (Buenos Aires) y j=2 (Córdoba);
@FOR(PLANTAS(JJ) | JJ #LT# 3:
    LITROS_CREMA(JJ) <= CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS(3,JJ)
);

! 10 - Restricción de capacidad para producto i=6 (manteca) en las plantas j=1 (Buenos Aires) y j=2 (Córdoba);
! La capacidad máxima de producto agrupado de índice 4 corresponde a la capacidad de producción 
! de manteca para las plantas j=1 (Buenos Aires) y j=2 (Córdoba);
@FOR(PLANTAS(JJ) | JJ #LT# 3:
    @SUM(CENTROS(K): Q(6,JJ,K)) <= CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS(4,JJ)
);

! 11 - Cantidad de leche en polvo producida en las plantas;
! Se calcula para los productos i=3 (leche en polvo entera) e i=4 (leche en polvo descremada)
! La capacidad máxima de producto agrupado de índice 2 corresponde a la capacidad de producción 
! de leche en polvo para cada planta;
@FOR(PLANTAS(JJ):
    S(3,JJ) + S(4,JJ) + 
    @SUM(CENTROS(K): (Q(3,JJ,K) + Q(4,JJ,K))) <= CAPACIDAD_MAXIMA_PRODUCTOS_AGRUPADOS(2,JJ)
);

! 12 - Restricciones de no negatividad;
@FOR(PLANTAS(JJ):
    LITROS_LECHE_DESCREMADA_SANTA_FE(JJ) >= 0
);

@FOR(PLANTAS(JJ):
    LITROS_ENTERA_A_DESCREMADA(JJ) >= 0
);

@FOR(PLANTAS(JJ):
    LITROS_CREMA(JJ) >= 0
);

@FOR(PRODUCTOS(II):
    @FOR(PLANTAS(JJ):
        @FOR(CENTROS(K):
            Q(II,JJ,K) >= 0
        )
    )
);

@FOR(PLANTAS(JJ):
    @FOR(PRODUCTOS(II):
        S(II,JJ) >= 0
    )
);




